group = "com.golanpiyush.yt_flutter_musicapi"
version = "1.0-SNAPSHOT"

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    flutterRoot = "C:/flutter"
}

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
        maven { url "https://chaquo.com/maven" }

    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath "com.chaquo.python:gradle:16.1.0"
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://chaquo.com/maven" }

    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.chaquo.python"


android {
    namespace = "com.golanpiyush.yt_flutter_musicapi"

    compileSdk = 35

    sourceSets {
        main {
            java.srcDirs += "src/main/kotlin"
            python.srcDir "src/main/python"
        }
        test.java.srcDirs += "src/test/kotlin"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 24
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

chaquopy {
    defaultConfig {
        version "3.9"
        pip {
            install "yt-dlp==2025.06.30" // Latest stable version as of July 2025
            install "ytmusicapi==1.10.3" // Latest stable version as of July 2025
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
        implementation 'androidx.annotation:annotation:1.7.1'

    implementation "com.fasterxml.jackson.core:jackson-databind:2.14.3"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0" // or latest version

    
    // Flutter dependencies - using local Flutter SDK
    compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-arm/flutter.jar")
    
    // Test dependencies
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.mockito:mockito-core:5.0.0")
}